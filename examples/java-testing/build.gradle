plugins {
    id 'java'
    id 'groovy' // needed for Spock testing framework
    id 'io.github.stewartbryson.snowflake' version '2.0.12'
    id 'com.adarshr.test-logger' version '3.2.0' // added for testing
    id 'com.github.ben-manes.versions' version '0.46.0'
}

repositories {
    mavenCentral()
    gradlePluginPortal() // added for functionalTest dependency
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

snowflake {
    connection = 'gradle_plugin'
    stage = 'upload'
    // Explained in the Ephemeral Cloning section
    useEphemeral = project.snowflake.isCI() // use ephemeral with CICD workflows
    keepEphemeral = project.snowflake.isPR() // keep ephemeral for PRs
    applications {
        add_numbers {
            inputs = ["a integer", "b integer"]
            returns = "string"
            handler = "Sample.addNum"
        }
    }
}

// Explained in the Testing section
testing {
    suites {
        test {
            useSpock('2.3-groovy-3.0')
        }
        functionalTest(JvmTestSuite) {
            targets {
                all {
                    useSpock('2.3-groovy-3.0')
                    dependencies {
                        implementation "io.github.stewartbryson:gradle-snowflake-plugin:2.0.12"
                    }
                    testTask.configure {
                        failFast true
                        // which SnowSQL connection to use
                        systemProperty 'connection', project.snowflake.connection
                        // if this is ephemeral, the test spec needs the name to connect to
                        if (project.snowflake.useEphemeral) {
                            systemProperty 'ephemeralName', snowflake.ephemeralName
                        }
                    }
                }
            }
        }
    }
}

version='0.1.0'

// empty task for matrix strategy execution in GitHub Actions
// please ignore
afterEvaluate {
    tasks.register('matrixStrategy') {
        description 'Empty task for matrix strategy execution in GitHub Actions.'
        dependsOn functionalTest
    }
}
